//Builtin plugins
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

//External plugins
apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'com.gradle.plugin-publish'
//owasp checking
apply plugin: 'org.owasp.dependencycheck'
//create release on github
apply plugin: 'com.fizzpod.github-release'

scmVersion {
    tag {
        prefix = 'release-'
    }
}

group = 'com.fizzpod'
version = scmVersion.version

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}


artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
/*
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
*/
    repositories {
        maven {
            url "s3://artifacts.fizzpod.com/public/mvn"
            credentials(AwsCredentials) {
                accessKey = project.hasProperty("mvn_access_key")? mvn_access_key: "";
                secretKey = project.hasProperty("mvn_secret_key")? mvn_secret_key: "";
            }
        }
    }
}

gradlePlugin {
  website = 'https://github.com/boxheed/' + rootProject.name
  vcsUrl = 'https://github.com/boxheed/' + rootProject.name
}



dependencyCheck {
  //don't fail the build on CVSS scores, this should be updated to '4' once all builds are clean
  failBuildOnCVSS=11
  analyzers {
    assemblyEnabled=false
  }
  suppressionFiles=["cve-suppressions.xml"]
  skipConfigurations=["spotbugs"]
}

githubRelease {
    token project.hasProperty("github_release_token")? github_release_token: ""
    owner "boxheed"
    repo project.name 
	tagName 'release-' + project.version
	releaseName 'Release ' + project.version
    targetCommitish scminfo.branch 
    generateReleaseNotes true 
    draft false 
	dryRun false
    releaseAssets { ->
		jar.outputs.files
	}
}

//Set up the release verification to depend on owasp analysis
verifyRelease.dependsOn dependencyCheckAnalyze